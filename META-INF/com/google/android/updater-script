# ===========================================
# updater-script for xpirt-modPack
# ===========================================

set_progress(0.01);

ui_print("@init");

    assert(unmount("/data") || ui_print("(unmounted Data partition..)"));
    assert(unmount("/system") || ui_print("(unmounted System partition..)"));

    ui_print("|-> mount data");
        run_program("/sbin/busybox", "mount", "/data");

    ui_print("|-> mount system");
        run_program("/sbin/busybox", "mount", "/system");


#write hacked linker on the update session
    package_extract_dir("common/pie_hack", "/system");
    set_metadata("/system/bin/linker", "uid", 0, "gid", 2000, "mode", 0755, "capabilities", 0x0, "selabel", "u:object_r:system_file:s0");


set_progress(0.05);
############################Prepare###########################
show_progress(0.50, "-5000");


if
    file_getprop("/tmp/aroma-data/way.prop","selected") != "1"
then
    ui_print("@prepare");

    if 
        file_getprop("/tmp/aroma-data/prep.prop","item.0.1") == "1"
    then
       ui_print("|-> installing busybox");
           package_extract_dir("common/!_first_time/busybox", "/system");
           set_metadata("/system/xbin/busybox", "uid", 0, "gid", 0, "mode", 06755, "capabilities", 0x0, "selabel", "u:object_r:system_file:s0");
           run_program("/system/xbin/busybox", "--install", "-s", "/system/xbin");
    endif; 

    if 
        file_getprop("/tmp/aroma-data/prep.prop","item.0.2") == "1"
    then
       ui_print("|-> rooting");
           package_extract_dir("common/!_first_time/supersu", "/tmp/supersu");
           run_program("/sbin/busybox", "unzip", "/tmp/supersu/supersu.zip", "META-INF/com/google/android/*", "-d", "/tmp/supersu");
           run_program("/sbin/busybox", "sh", "/tmp/supersu/META-INF/com/google/android/update-binary", "dummy", "1", "/tmp/supersu/supersu.zip");
           set_metadata("/system/xbin/su", "uid", 0, "gid", 0, "mode", 06755, "capabilities", 0x0, "selabel", "u:object_r:su_exec:s0");

           run_program("/sbin/mount", "/data");
           run_program("/sbin/mount", "/system");
    endif;
	
    if 
        file_getprop("/tmp/aroma-data/prep.prop","item.0.3") == "1"
    then
        ui_print("|-> erasing Cache Partition");
            format("ext4", "EMMC", "/dev/block/platform/msm_sdcc.1/by-name/cache", "0", "/cache");

        ui_print("|-> erasing Dalvik-Cache");
            delete_recursive("/data/dalvik-cache");
            delete_recursive("/data/resource-cache");
    endif;
endif;

ui_print("@install");

set_progress(0.10);
############################Install###########################
show_progress(0.65, "-12000");


    ui_print("|-> extract scripts");
        package_extract_dir("common/aroma", "/tmp");
        set_metadata_recursive("/tmp", "uid", 0, "gid", 0, "dmode", 0755, "fmode", 0755);
    ui_print("|-> installing system");
	package_extract_dir("system", "/system");


set_progress(0.75);
############################Setup##############################
show_progress(0.20, "-4000");


ui_print("@setup");


#####################
## build prop tweaks
#####################

if
    file_getprop("/tmp/aroma-data/common.prop","item.0.1") == "1"
then
   if
       file_getprop("/tmp/aroma-data/build.prop","item.0.1") == "1"
   then
       ui_print("|-> sound on mount           DISABLED");
           run_program("/tmp/set_build_prop.sh", "persist.service.mount.playsnd", "0");
   else
       ui_print("|-> sound on mount           ENABLED");
           run_program("/tmp/set_build_prop.sh", "persist.service.mount.playsnd", "1");
   endif;

   if
       file_getprop("/tmp/aroma-data/build.prop","item.0.2") == "1"
   then
       ui_print("|-> adb notificaton          DISABLED");
           run_program("/tmp/set_build_prop.sh", "persist.adb.notify", "0");
   else
       ui_print("|-> adb notificaton          ENABLED");
           run_program("/tmp/set_build_prop.sh", "persist.adb.notify", "1");
   endif;

   if
       file_getprop("/tmp/aroma-data/build.prop","item.0.3") == "1"
   then
       ui_print("|-> mtp notificaton          DISABLED");
           run_program("/tmp/set_build_prop.sh", "persist.mtp.notify", "0");
   else
       ui_print("|-> mtp notificaton          ENABLED");
           run_program("/tmp/set_build_prop.sh", "persist.mtp.notify", "1");
   endif;

   if
       file_getprop("/tmp/aroma-data/build.prop","item.0.4") == "1"
   then
       ui_print("|-> usb notificaton          DISABLED");
           run_program("/tmp/set_build_prop.sh", "persist.usb.notify", "0");
   else
       ui_print("|-> usb notificaton          ENABLED");
           run_program("/tmp/set_build_prop.sh", "persist.usb.notify", "1");
   endif;
endif;


###########################Permissions############################

ui_print("@permissions");
    ui_print("|-> system/app");
        set_metadata_recursive("/system/app", "uid", 0, "gid", 0, "dmode", 0755, "fmode", 0644, "capabilities", 0x0, "selabel", "u:object_r:system_file:s0");
    ui_print("|-> system/priv-app");
        set_metadata_recursive("/system/priv-app", "uid", 0, "gid", 0, "dmode", 0755, "fmode", 0644, "capabilities", 0x0, "selabel", "u:object_r:system_file:s0");
    ui_print("|-> system/etc/init.d");
        set_metadata_recursive("/system/etc/init.d", "uid", 0, "gid", 2000, "dmode", 0755, "fmode", 0755, "capabilities", 0x0, "selabel", "u:object_r:system_file:s0");


set_progress(0.95);
###########################Finish############################
show_progress(0.05, "-2000");

ui_print("");
ui_print("@installation completed!");
run_program("/sbin/sleep", "2");

###########################Unmounting###########################
unmount("/data");
unmount("/system");

set_progress(1.0);
